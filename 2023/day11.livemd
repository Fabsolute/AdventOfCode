# Day 11 - Cosmic Expansion

```elixir
Mix.install([:kino, :eastar])

default = "...#......
.......#..
#.........
..........
......#...
.#........
.........#
..........
.......#..
#...#....."
```

## Problem

[https://adventofcode.com/2023/day/11](https://adventofcode.com/2023/day/11)

### Part One

You continue following signs for "Hot Springs" and eventually come across an [observatory](https://en.wikipedia.org/wiki/Observatory). The Elf within turns out to be a researcher studying cosmic expansion using the giant telescope here.

He doesn't know anything about the missing machine parts; he's only visiting for this research project. However, he confirms that the hot springs are the next-closest area likely to have people; he'll even take you straight there once he's done with today's observation analysis.

Maybe you can help him with the analysis to speed things up?

The researcher has collected a bunch of data and compiled the data into a single giant _image_ (your puzzle input). The image includes _empty space_ (`.`) and _galaxies_ (`#`). For example:

```
...#......
.......#..
#.........
..........
......#...
.#........
.........#
..........
.......#..
#...#.....
```

The researcher is trying to figure out the sum of the lengths of the _shortest path between every pair of galaxies_. However, there's a catch: the universe expanded in the time it took the light from those galaxies to reach the observatory.

Due to something involving gravitational effects, _only some space expands_. In fact, the result is that _any rows or columns that contain no galaxies_ should all actually be twice as big.

In the above example, three columns and two rows contain no galaxies:

```
   v  v  v
 ...#......
 .......#..
 #.........
>..........<
 ......#...
 .#........
 .........#
>..........<
 .......#..
 #...#.....
   ^  ^  ^
```

These rows and columns need to be _twice as big_; the result of cosmic expansion therefore looks like this:

```
....#........
.........#...
#............
.............
.............
........#....
.#...........
............#
.............
.............
.........#...
#....#.......
```

Equipped with this expanded universe, the shortest path between every pair of galaxies can be found. It can help to assign every galaxy a unique number:

```
....1........
.........2...
3............
.............
.............
........4....
.5...........
............6
.............
.............
.........7...
8....9.......
```

In these 9 galaxies, there are _36 pairs_. Only count each pair once; order within the pair doesn't matter. For each pair, find any shortest path between the two galaxies using only steps that move up, down, left, or right exactly one `.` or `#` at a time. (The shortest path between two galaxies is allowed to pass through another galaxy.)

For example, here is one of the shortest paths between galaxies `5` and `9`:

```
....1........
.........2...
3............
.............
.............
........4....
.5...........
.##.........6
..##.........
...##........
....##...7...
8....9.......
```

This path has length _`9`_ because it takes a minimum of _nine steps_ to get from galaxy `5` to galaxy `9` (the eight locations marked `#` plus the step onto galaxy `9` itself). Here are some other example shortest path lengths:

* Between galaxy `1` and galaxy `7`: 15
* Between galaxy `3` and galaxy `6`: 17
* Between galaxy `8` and galaxy `9`: 5

In this example, after expanding the universe, the sum of the shortest path between all 36 pairs of galaxies is _`374`_.

Expand the universe, then find the length of the shortest path between every pair of galaxies. _What is the sum of these lengths?_

<!-- livebook:{"break_markdown":true} -->

### PART 2

The galaxies are much _older_ (and thus much _farther apart_) than the researcher initially estimated.

Now, instead of the expansion you did before, make each empty row or column _<span title="And you have to have your pinky near your mouth when you do it.">one million</span> times_ larger. That is, each empty row should be replaced with `1000000` empty rows, and each empty column should be replaced with `1000000` empty columns.

(In the example above, if each empty row or column were merely `10` times larger, the sum of the shortest paths between every pair of galaxies would be _`1030`_. If each empty row or column were merely `100` times larger, the sum of the shortest paths between every pair of galaxies would be _`8410`_. However, your universe will need to expand far beyond these values.)

Starting with the same initial image, expand the universe according to these new rules, then find the length of the shortest path between every pair of galaxies. _What is the sum of these lengths?_

## Solution

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input = Kino.Input.textarea("Image", default: default)
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
values =
  Kino.Input.read(input)
  |> String.split("\n", trim: true)
  |> Enum.map(&String.codepoints/1)
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Cosmos do
  defp apply_coordinates(image) do
    image
    |> Enum.with_index()
    |> Enum.map(fn {row, y} ->
      row
      |> Enum.with_index()
      |> Enum.map(fn {column, x} -> {{y, x}, column} end)
    end)
  end

  defp expand([], _, _, _, acc), do: Enum.reverse(acc)

  defp expand([row | rest], size, offset, pos, acc) do
    offset =
      if Enum.all?(row, &(elem(&1, 1) == ".")) do
        offset + size
      else
        offset
      end

    expand(rest, size, offset, pos, [
      row
      |> Enum.map(fn {coord, v} ->
        {
          coord
          |> Tuple.insert_at(pos, elem(coord, pos) + offset)
          |> Tuple.delete_at(pos + 1),
          v
        }
      end)
      | acc
    ])
  end

  defp transpose(array) do
    array
    |> Enum.zip()
    |> Enum.map(&Tuple.to_list/1)
  end

  defp expand(image, expansion_size) do
    image
    |> expand(expansion_size - 1, 0, 0, [])
    |> transpose()
    |> expand(expansion_size - 1, 0, 1, [])
    |> transpose()
  end

  defp find_galaxies(coords) do
    coords
    |> List.flatten()
    |> Enum.filter(&(&1 |> elem(1) == "#"))
    |> Enum.map(&(&1 |> elem(0)))
  end

  defp find_combination(_, 0), do: [[]]
  defp find_combination([], _), do: []

  defp find_combination([galaxy | rest], current) do
    rest
    |> find_combination(current - 1)
    |> Enum.map(&[galaxy | &1])
    |> Enum.concat(find_combination(rest, current))
  end

  defp find_distances(pairs) do
    pairs
    |> Enum.map(fn [{y1, x1}, {y2, x2}] ->
      abs(x1 - x2) + abs(y1 - y2)
    end)
  end

  def get_result(values, expansion) do
    values
    |> apply_coordinates()
    |> expand(expansion)
    |> find_galaxies()
    |> find_combination(2)
    |> find_distances()
    |> Enum.sum()
  end
end
```

### PART 1

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
values
|> Cosmos.get_result(2)
```

### PART 2

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
values
|> Cosmos.get_result(1_000_000)
```
