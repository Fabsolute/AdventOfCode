# Day 1 - Trebuchet

```elixir
Mix.install([:kino])

default = "1abc2
pqr3stu8vwx
a1b2c3d4e5f
treb7uchet"
```

## Problem

[https://adventofcode.com/2023/day/1](https://adventofcode.com/2023/day/1)

### PART 1

Something is wrong with global snow production, and you've been selected to take a look. The Elves have even given you a map; on it, they've used stars to mark the top fifty locations that are likely to be having problems.

You've been doing this long enough to know that to restore snow operations, you need to check all _fifty stars_ by December 25th.

Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants _one star_. Good luck!

You try to ask why they can't just use a [weather machine](https://adventofcode.com/2015/day/1) ("not powerful enough") and where they're even sending you ("the sky") and why your map looks mostly blank ("you sure ask a lot of questions") <span title="My hope is that this abomination of a run-on sentence somehow conveys the chaos of being hastily loaded into a trebuchet.">and</span> hang on did you just say the sky ("of course, where do you think snow comes from") when you realize that the Elves are already loading you into a [trebuchet](https://en.wikipedia.org/wiki/Trebuchet) ("please hold still, we need to strap you in").

As they're making the final adjustments, they discover that their calibration document (your puzzle input) has been _amended_ by a very young Elf who was apparently just excited to show off her art skills. Consequently, the Elves are having trouble reading the values on the document.

The newly-improved calibration document consists of lines of text; each line originally contained a specific _calibration value_ that the Elves now need to recover. On each line, the calibration value can be found by combining the _first digit_ and the _last digit_ (in that order) to form a single _two-digit number_.

For example:

```
1abc2
pqr3stu8vwx
a1b2c3d4e5f
treb7uchet
```

In this example, the calibration values of these four lines are `12`, `38`, `15`, and `77`. Adding these together produces _`142`_.

Consider your entire calibration document. _What is the sum of all of the calibration values?_

<!-- livebook:{"break_markdown":true} -->

### PART 2

Your calculation isn't quite right. It looks like some of the digits are actually _spelled out with letters_: `one`, `two`, `three`, `four`, `five`, `six`, `seven`, `eight`, and `nine` _also_ count as valid "digits".

Equipped with this new information, you now need to find the real first and last digit on each line. For example:

```
two1nine
eightwothree
abcone2threexyz
xtwone3four
4nineeightseven2
zoneight234
7pqrstsixteen
```

In this example, the calibration values are `29`, `83`, `13`, `24`, `42`, `14`, and `76`. Adding these together produces _`281`_.

_What is the sum of all of the calibration values?_

## Solution

```elixir
input = Kino.Input.textarea("Calibrations", default: default)
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
values =
  Kino.Input.read(input)
  |> String.split("\n", trim: true)
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Trebuchet do
  @replaces %{
    "one" => "1",
    "two" => "2",
    "three" => "3",
    "four" => "4",
    "five" => "5",
    "six" => "6",
    "seven" => "7",
    "eight" => "8",
    "nine" => "9"
  }

  defp calibrate_line(value, regex) do
    Regex.scan(regex, value, capture: :all_but_first)
    |> List.flatten()
    |> then(&(List.first(&1) <> List.last(&1)))
    |> String.replace(Map.keys(@replaces), fn x -> @replaces[x] end)
    |> String.to_integer()
  end

  def calibrate(values, regex) do
    values
    |> Enum.map(&calibrate_line(&1, regex))
    |> Enum.sum()
  end
end
```

### PART 1

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
values
|> Trebuchet.calibrate(~r/(?=(\d))/)
```

### PART 2

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
values
|> Trebuchet.calibrate(~r/(?=(\d|one|two|three|four|five|six|seven|eight|nine))/)
```
